integer = @{ ASCII_DIGIT+ }

arity = ${ integer ~ "->" ~ integer }

open_func  = { "(" }
close_func = { ")" }

apply = { "!" }

quote = ${ "'" ~ identifier? }

bind = ${ "$" ~ identifier }

identifier = @{ "?" | "=" | (ASCII_ALPHA ~ (ASCII_ALPHANUMERIC | "_" | "-")*) }

/// literal has precedence before identifier
expression = _{
    arity
  | integer
  | open_func
  | close_func
  | apply
  | quote
  | bind
  | identifier
}

program = _{
    SOI ~ expression* ~ EOI
}

WHITESPACE = _{ NEWLINE | WHITE_SPACE }
COMMENT    = _{ "#" ~ (!"\n" ~ ANY)* }
